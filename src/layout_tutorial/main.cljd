(ns layout-tutorial.main
  "Faithful port of https://docs.flutter.dev/development/ui/interactive"
  ;; pure dart package are imported using string
  (:require ["package:flutter/material.dart" :as material]
            ["package:flutter/widgets.dart" :as widgets]
            ["package:flutter/painting.dart" :as painting]
            ["package:flutter/rendering.dart" :as rendering]
            ["dart:ui" :as ui]
            [cljd.flutter.alpha :as flutter]))

(def primary-color
  material.Colors/blue)

(def image-section
  (widgets.Image/asset "images/lake.jpg"
                       :width 600
                       :height 240
                       :fit painting.BoxFit/cover))

(def favorite-widget-state
  (flutter/widget
   :state [state-atom {:favorited true
                       :counter 41}]
   (widgets/Row
    :mainAxisSize rendering.MainAxisSize/min
    :children [(flutter/nest
                (widgets/Container
                 :padding (painting.EdgeInsets/all 0))
                (material/IconButton
                 :padding (painting.EdgeInsets/all 0)
                 :alignment painting.Alignment/centerRight
                 :icon (if (:favorited @state-atom)
                         (widgets/Icon material.Icons/star)
                         (widgets/Icon material.Icons/star_border))
                 :color material.Colors/red
                 :onPressed #(if (:favorited @state-atom)
                               (do
                                 (swap! state-atom update :counter dec)
                                 (swap! state-atom update :favorited not))
                               (do
                                 (swap! state-atom update :counter inc)
                                 (swap! state-atom update :favorited not)))))
               (flutter/nest
                (widgets/SizedBox
                 :width 18)
                (widgets/SizedBox)
                (widgets/Text (str (:counter @state-atom))))])))

(def title-section
  (flutter/nest
   (widgets/Container
    :padding (painting.EdgeInsets/all 32))
   (widgets/Row
    :children [(flutter/nest
                widgets/Expanded
                (widgets/Column
                 :crossAxisAlignment rendering.CrossAxisAlignment/start
                 :children [(flutter/nest
                             (widgets/Container
                              :padding (painting.EdgeInsets/only :bottom 8))
                             (widgets/Text "Oeschinen Lake Campground"
                                           :style (painting/TextStyle
                                                   :fontWeight (painting.FontWeight/bold))))
                            (widgets/Text "Kandersteg, Switzerland"
                                          :style (painting/TextStyle
                                                  :color material.Colors/grey))]))
               favorite-widget-state])))

(defn build-button-column [color icon text]
  (widgets/Column
   :mainAxisSize rendering.MainAxisSize/min
   :mainAxisAlignment rendering.MainAxisAlignment/center
   :children [(widgets/Icon icon
                            :color primary-color)
              (flutter/nest
               (widgets/Container
                :margin (painting.EdgeInsets/only :top 8))
               (widgets/Text text
                             :style (painting/TextStyle
                                     :fontSize 12
                                     :fontWeight ui.FontWeight/w400
                                     :color color)))]))

(def button-section
  (widgets/Row
   :mainAxisAlignment (. rendering/MainAxisAlignment spaceEvenly)
   :children [(build-button-column primary-color material.Icons/call "CALL")
              (build-button-column primary-color material.Icons/near_me "ROUTE")
              (build-button-column primary-color material.Icons/share "SHARE")]))

(def text-section
  (flutter/nest
   (widgets/Padding
    :padding (painting.EdgeInsets/all 32))
   (widgets/Text
    (str "Lake Oeschinen lies at the foot of the Bl√ºemlisalp in the Bernese "
         "Alps. Situated 1,578 meters above sea level, it is one of the "
         "larger Alpine Lakes. A gondola ride from Kandersteg, followed by a "
         "half-hour walk through pastures and pine forest, leads you to the "
         "lake, which warms to 20 degrees Celsius in the summer. Activities "
         "enjoyed here include rowing, and riding the summer toboggan run.")
    :softWrap true)))

(defn main []
  (material/runApp
   (material/MaterialApp
    :title "Flutter layout demo"
    :theme (material/ThemeData :primarySwatch primary-color)
    :home (material/Scaffold
           :appBar (material/AppBar
                    :title (widgets/Text "Flutter layout demo"))
           :body (widgets/ListView
                  :children [image-section
                             title-section
                             button-section
                             text-section])))))